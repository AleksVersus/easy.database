QSP-Game проверка существования строки в таблице
$args[0] - идентификатор таблицы данных
$args[1] - идентификатор или номер строки
# edb.row.isThere
$args[1]=$args[1]	& !@ идентификатор строки
$args['id_column']=$arritem('$'+$args[0],'id_column')	& !@ колонка идентификаторов
$args['first_col']=$args[0]+'_'+@em.str.getWord($arritem('$'+$args[0],'columns'),1)	& !@ самая первая колонка в таблице данных
if $args['first_col']<>'':
	!@ если в таблице данных есть хоть одна колонка
	if strcomp($args[1],'[123456789]\d*'):
		!@ если передано число
		if $args['id_column']<>'':
			!@ если существует колонка идентификаторов
			if arrpos('$'+$args[0]+'_'+$args['id_column'],$args[1])<>-1:
				!@ и данный идентификатор присутствует в колонке, можно вернуть true - такая строка существует
				result=2	& !@ в данном случае двойка показывает, что это именно идентификатор
			elseif val($args[1])<arrsize($args[0]+'_'+$args['id_column']):
				!@ если идентификатор не присутствует в колонке, значит мы имеем дело с числом
				!@ и если оно попадает в высоту колонки, снова возвращаем true
				result=1
			else
				!@ если это не идентификатор, или это число, выходящее за пределы высоты колонки, возвращаем false
				result=0
			end
		elseif val($args[1])<arrsize($args['first_col']):
			!@ если колонка идентификаторов не существует, но число попадает
			!@ в высоту самой первой колонки, тоже можно считать, что работаем со строкой
			result=1
		end
	else
		!@ если это не число
		if $args['id_column']<>'' and arrpos('$'+$args[0]+'_'+$args['id_column'],$args[1])<>-1:
			!@ если колонка идентификаторов существует и данный идентификатор присутствует в нём
			result=2	& !@ возвращаем два, то есть указываем, что это именно идентификатор
		else
			!@ в противном случае, это не строка таблицы данных
			result=0
		end
	end
else
	!@ если нет колонок, нет и строк
	result=0
end
--- edb.row.isThere ---------------------------------

Это не метод, это вспомогательная функция
