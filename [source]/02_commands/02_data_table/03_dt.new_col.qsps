QSP-Game Создаём колонку в таблице данных

!@ $0 - идентификатор колонки
!@ $1 - тип колонки
!@ $2 - идентификатор таблицы данных
# edb.dt.new_col
if $args[2] = '': $args[2] = $EASY_DATABASE['current_data_table']
!@pp:if(edb_fuse):include
if $EASY_DATABASE['ID'] = '':	msg "dt.new_col: Ошибка. База данных не инициализирована." & exit
if $args[2] = '':	msg 'dt.new_col: Ошибка. Не указан идентификатор таблицы данных.' & exit
if no @edb.list.is_el($EASY_DATABASE['data_tables_ids'], $args[2]):	msg 'dt.new_col: Ошибка. Таблица данных `<<$args[2]>>` не существует.' & exit
if $args[0] = '': msg 'dt.new_col: Ошибка. Не указан идентификатор колонки.' &	exit
if $args[0] = 'id' or $args[1] = 'ids':	msg 'dt.new_col: Колонка первичных ключей уже создана.' & exit
if instr('$%', $mid($args[0], 1, 1)): msg 'dt.new_col: Недопустимо указывать префикс типа → <<$args[0]>>.' & exit
!@pp:endif
!@ значения по умолчанию:
if $args[1] = '': $args[1] = 'str'
!@ тип данных для колонки задаётся в массиве, описывающем таблицу данных.
dynamic "$<<$args[2]>>['column.<<$args[0]>>.type'] = '<<$args[1]>>'"
!@ если размер колонки первичных ключей больше нуля, циклом заполняем указанную колонку пустыми значениями.
!@ Внимание! Лучше заранее предусмеотреть необходимое количество колонок и создавать их все на старте игры,
!@ в противном случае, при создании колонок во время игры, данный алгоритм будет подвешивать плеер при больших объёмах данных.
args[20] = @edb.dt.height($args[2])
if args[20] > 0:
	loop args[21] = 0 while args[21] < args[20] step args[21] += 1:
		$args[22] = $arritem('$<<$args[2]>>_id', args[21])
		if $args[1] = 'tuple':
			dynamic "%<<$args[2]>>_<<$args[0]>>['<<$args[22]>>'] = []"
		elseif $args[1] = 'num':
			dynamic "<<$args[2]>>_<<$args[0]>>['<<$args[22]>>'] = 0"
		else
			dynamic "$<<$args[2]>>_<<$args[0]>>['<<$args[22]>>'] = ''"
		end
	end
end
!@ добавляем колонку в список колонок
dynamic "$<<$args[2]>>['columns'] = @edb.list.append($<<$args[2]>>['columns'], '<<$args[0]>>')"
!@ делаем колонку текущей
@edb.col.set_cur($args[0], $args[2])
--- edb.dt.new_col ---------------------------------

args[20] - размер массива первичных ключей
args[21] - счётчик цикла i
$args[22] - идентификатор строки