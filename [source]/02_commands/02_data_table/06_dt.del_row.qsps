QSP-Game Удаляет строку из таблицы данных по её идентификатору

!@ $0 - идентификатор строки (primary key)
!@ $1 - идентификатор таблицы данных
# edb.dt.del_row
if $args[1] = '': $args[1] = $EASY_DATABASE['current_data_table']
!@pp:if(edb_fuse):include
if $EASY_DATABASE['ID'] = '': msg "dt.del_row: Ошибка. База данных не инициализирована." & exit
if $args[1] = '': msg "dt.del_row: Ошибка. Не указан идентификатор таблицы данных." & exit
if no @edb.list.is_el($EASY_DATABASE['data_tables_ids'], $args[1]): msg "dt.del_row: Ошибка. Таблица данных `<<$args[1]>>` не существует." & exit
!@pp:endif
if $args[0] = '': $args[0] = $arritem('$<<$args[1]>>', 'current_row')
!@ Удалить строку, которой нет в таблице данных, нельзя, однако ошибку в игре это не вызовет.
if no @edb.row.is($args[0], $args[1]): exit
!@ Удаление строки производится путём удаления ячеек из каждого массива-столбца:
$args[21] = @edb.dt.columns($args[1])
$args[22] = { killvar '$<<$args[3]>>_<<$args[0]>>', $args[4] }
@edb.list.for_each($args[21], $args[22], $args[1], $args[0])
!@ если строка была текущей, снимаем указатель
if $arritem('$<<$args[1]>>', 'current_row') = $args[0]:
	dynamic "$<<$args[1]>>['current_row'] = ''"
end
!@ делаем таблицу данных текущей
@edb.dt.set_cur($args[1])
--- edb.dt.del_row ---------------------------------

!@ $20 - тип первичного ключа
!@ $21 - список колонок
!@ $22 - функция типа "колбэк" для обработки элементов списка

{:for_each
	0 - column list
	1 - callback
	2 - table
	3 - primary key
	{:callback
		0 - item
		1 - tail
		2 - result
		3 - table
		4 - primary key
	}
}