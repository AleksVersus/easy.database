QSP-Game Очистка таблицы данных

!@ $0 - идентификатор новой таблицы данных
!@ $1 - идентификатор исходной таблицы данных
!@ $2 - идентификатор строки или номер, с которого начинается клонирование таблицы данных
!@ #3 - число элементов к копированию (по умолчанию все оставшиеся.)
# edb.dt.clone
if $args[0] = '': msg "edb.dt.clone: Ошибка. Не указан идентификатор приёмника." & exit
if $args[1] = '': $args[1] = $EASY_DATABASE['current_data_table']
!@pp:if(edb_fuse):include
if $EASY_DATABASE['ID'] = '': msg "edb.dt.clone: Ошибка. База данных не инициализирована." & exit
if $args[1] = '': msg "edb.dt.clone: Ошибка. Не указан идентификатор таблицы данных." & exit
if no @edb.list.is_el($EASY_DATABASE['data_tables_ids'], $args[1]): msg "edb.dt.clone: Ошибка. Таблица данных `<<$args[1]>>` не существует." & exit
!@pp:endif
!@ получаем номер элемента, с которого мы начинаем копирование таблицы данных
if no ($args[2] = '' or $arritem('$<<$args[1]>>_id', $args[2]) <> $args[2]):
	args[2] = arrpos('$<<$args[1]>>_id', $args[2])
end
!@ получаем число элементов к копированию
if args[3] = 0: args[3] = arrsize('$<<$args[1]>>_id') - args[2]
!@ получаем список колонок исходной таблицы
$args[20] = $arritem('$<<$args[1]>>', 'columns')
!@ колбэк
$args[21] = {
	args[5], args[6] = val($args[5]), val($args[6])
	copyarr '<<$args[3]>>_<<$args[0]>>', '<<$args[4]>>_<<$args[0]>>', args[5], args[6]
}
!@ циклически копируем колонки
@edb.list.for_each($args[20], $args[21], $args[0], $args[1], $str(args[2]), $str(args[3]))
!@ копируем массив, описывающий таблицу данных
copyarr $args[0], $args[1]
!@ заменяем некоторые значения:
dynamic "
!@pp:удаляем идентификатор текущей колонки
$<<$args[0]>>['current_col'] = ''
!@pp:удаляем идентификатор текущей строки
$<<$args[0]>>['current_row'] = ''
!@pp:меняем идентификатор таблицы данных
$<<$args[0]>>['ID'] = '<<$args[0]>>'
"
!@ добавляем таблицу в список таблиц данных
$EASY_DATABASE['data_tables_ids'] = @edb.list.append($EASY_DATABASE['data_tables_ids'], $args[0])
!@ увеличиваем счётчик созданных таблиц данных:
EASY_DATABASE['data_tables_counter'] += 1
!@ увеличиваем число существующих таблиц данных:
EASY_DATABASE['data_tables_number'] += 1
!@ делаем новую таблицу данных текущей
@edb.dt.set_cur($args[0])
--- edb.dt.clone ---------------------------------

$args[]:
	20 - список колонок
	21 - колбэк очистки

{:for_each
	0 - column list
	1 - callback
	2 - '|'
	3 - new-table
	4 - old-table
	5 - start number
	6 - number of copyed els
	{:callback
		0 - item (column)
		1 - tail
		2 - result
		3 - new-table (1)
		4 - old-table (2)
		5 - start number (3)
		6 - number of copyed els (4)
	}
}