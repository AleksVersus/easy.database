QSP-Game Прибавляет значение ячейке

!@ $0 - значение, прибавляемое к ячейке
!@ $1 - идентификатор строки
!@ $2 - идентификатор колонки
!@ $3 - идентификатор таблицы данных
# edb.cell.add
if $args[3] = '': $args[3] = $EASY_DATABASE['current_data_table']
!@pp:if(edb_fuse):include
if $EASY_DATABASE['ID'] = '': msg "cell.add: Ошибка. База данных не инициализирована." & exit
if $args[3] = '': msg "cell.add: Ошибка. Не указан идентификатор таблицы данных." & exit
if no @edb.list.is_el($EASY_DATABASE['data_tables_ids'], $args[3]): msg "cell.add: Ошибка. Таблица данных `<<$args[2]>>` не существует." & exit
!@pp:endif
if $args[1] = '': $args[1] = $arritem('$<<$args[3]>>', 'current_row')
if $args[2] = '': $args[2] = $arritem('$<<$args[3]>>', 'current_col')
!@ Присвоить значение ячейке, которая не существует не получится, однако ошибку это не вызовет
if no @edb.cell.is($args[1], $args[2], $args[3]): exit
!@ так же невозможно присвоить значение ячейке, которая находится в столбце первичных ключей
if $args[2] = 'id': exit
$args[20] = $arritem('$<<$args[3]>>','column.<<$args[2]>>.type')
!@ непосредственно добавляем значение ячейке
if $args[20] = 'tuple':
!@pp:if(edb_fuse):include
!@ предохранитель, не вызывающий ошибку
if %args[0] = []: "cell.add: Передан пустой кортеж. К ячейкам с типом `tuple` нельзя добавлять строки ('<<$args[0]>>') или числа (<<args[0]>>)"
!@pp:endif
	dynamic "
		%<<$args[3]>>_<<$args[2]>>['<<$args[1]>>'] = (%args[0] & %args[1])
	", %arritem('%<<$args[3]>>_<<$args[2]>>', $args[1]), %args[0]
elseif $args[20] = 'num':
	!@ увеличиваем значение на указанное число
	dynamic "<<$args[3]>>_<<$args[2]>>['<<$args[1]>>'] += <<args[0]>>"
elseif $args[20] = 'list':
	!@ если тип колонки список, добавляем в список новый элемент
	dynamic "
		$<<$args[3]>>_<<$args[2]>>['<<$args[1]>>'] = $args[0]
	", @edb.list.append($arritem('$<<$args[3]>>_<<$args[2]>>', $args[1]), $args[0])
elseif $args[20] = 'code':
	dynamic "
		$<<$args[3]>>_<<$args[2]>>['<<$args[1]>>'] += @em.RN + $args[0]
	", $args[0]
elseif $args[20] = 'str':
	!@ для строки это просто конкатенация
	jump 'string_and_other'
elseif $args[20] = 'dict':
	!@ TODO: для словаря пока что это просто конкатенация
	jump 'string_and_other'
else:
	:string_and_other
	dynamic "
		$<<$args[3]>>_<<$args[2]>>['<<$args[1]>>'] += $args[0]
	", $args[0]
end
!@ делаем строку и столбец текущими
@edb.row.set_cur($args[1])
@edb.col.set_cur($args[2])
--- edb.cell.add ---------------------------------

!@ $20 - тип колонки
!@ $21 - значение, если передан кортеж
!@ #22 - индекс в кортеже, если передан кортеж
