QSP-Game Удаляет значение из ячейки

$args[0] или args[0] - значение, удаляемое из ячейки
$args[1] - идентификатор строки
$args[2] - идентификатор колонки
$args[3] - идентификатор таблицы данных

# edb.cell.del
if $args[3] = '': $args[3] = $EASY_DATABASE['current_data_table']
!@pp:if(edb_fuse):include
if $EASY_DATABASE['ID'] = '': msg "cell.del: Ошибка. База данных не инициализирована." & exit
if no @edb.list.is_el($EASY_DATABASE['data_tables_ids'], $args[3]): msg "cell.del: Ошибка. Таблица данных `<<$args[3]>>` не существует." & exit
if $args[3] = '': msg "cell.del: Ошибка. Не указан идентификатор таблицы данных." & exit
!@pp:endif
if $args[1] = '': $args[1] = $arritem('$<<$args[3]>>', 'current_row')
if $args[2] = '': $args[2] = $arritem('$<<$args[3]>>', 'current_col')
!@ Удалить значение из ячейки, которая не существует не получится, однако ошибку это не вызовет
if $arritem('$<<$args[3]>>_id', $args[1]) <> $args[1] or arrsize('$<<$args[3]>>_<<$args[2]>>') = 0: exit
!@ так же невозможно удалить значение из ячейки, которая находится в столбце первичных ключей
if $args[2] = 'id' or $arritem('$<<$args[3]>>_<<$args[2]>>', 0) = 'ids': exit
$args[20] = $arritem('$<<$args[3]>>_<<$args[2]>>', 0)
!@ непосредственно удаляем значение из ячейки
if $args[20] = 'num':
	!@ уменьшаем значение на указанное число
	dynamic "<<$args[3]>>_<<$args[2]>>['<<$args[1]>>'] -= <<args[0]>>"
elseif $args[20] = 'list':
	!@ если тип колонки список, убираем элемент из списка
	dynamic "
		$<<$args[3]>>_<<$args[2]>>['<<$args[1]>>'] = @edb.list.remove($<<$args[3]>>_<<$args[2]>>['<<$args[1]>>'], '<<$args[0]>>')
	"
elseif $args[20] = 'tuple':
	!@ если тип колонки кортеж, уменьшить число его элементов нельзя,
	!@ зато можно изменить значения уже существующих элементов
	!@ в args[0] передаются либо значение, либо номер удаляемого элемента
	local $edb_cell_add_temp_array_
	@em.str.inArr($arritem('$<<$args[3]>>_<<$args[2]>>', '<<$args[1]>>'), '$edb_cell_add_temp_array_', '')
	if $args[0] <> '':
		!@ если передана не пустая строка, значит требуется удалить значение
		!@ ищем позицию в массиве и заменяем значение
		args[21] = arrpos('$edb_cell_add_temp_array_', $args[0])
		if args[21] <> -1:
			$edb_cell_add_temp_array_[args[21]] = ''
			dynamic "
				$<<$args[3]>>_<<$args[2]>>['<<$args[1]>>'] = @em.arr.print('$edb_cell_add_temp_array_', '[only result] [not last]', '')
			"
		end
	else:
		!@ если передана пустая строка, значит удаляем элемент по позиции
		$edb_cell_add_temp_array_[args[0]] = ''
		dynamic "
			$<<$args[3]>>_<<$args[2]>>['<<$args[1]>>'] = @em.arr.print('$edb_cell_add_temp_array_', '[only result] [not last]', '')
		"
	end
elseif $args[20] = 'str':
	!@ в строке просто удаляем первое найденное вхождение
	jump 'string_and_other'
elseif $args[20] = 'dict':
	!@ TODO: для словаря пока что это то же, что и для строки
	jump 'string_and_other'
elseif $args[20] = 'code':
	!@ удалить код напрямую нельзя.
	!@ PASS
else:
	:string_and_other
	$args[22] = $arritem('$<<$args[3]>>_<<$args[2]>>', $args[1])
	args[23] = instr($args[22], $args[0])
	if args[23] <> 0:
		$args[22] = $mid($args[22], 1, args[23]-1) + $mid($args[22], args[23]+len($args[0]))
		dynamic "$<<$args[3]>>_<<$args[2]>>['<<$args[1]>>'] = '<<$args[22]>>'"
	end
end
!@ делаем строку и столбец текущими
@edb.row.set_cur($args[1])
@edb.col.set_cur($args[2])
--- edb.cell.del ---------------------------------

$args[20] - тип колонки
args[21] - позиция во временном массиве
$args[22] - строка к разбору
args[23] - номер символа, с которого подстрока входит в строку


!@pp:if(build_easy_math!=Off):nopp
!@pp:var(em_arr_print)
!@pp:var(em_str_inarr)
!@pp:endif
