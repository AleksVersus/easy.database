QSP-Game Извлекает значение ячейки

!@ $0 - идентификатор строки
!@ $1 - идентификатор колонки
!@ $2 - идентификатор таблицы данных

# edb.cell.get_value
if $args[2] = '': $args[2] = $EASY_DATABASE['current_data_table']
!@pp:if(edb_fuse):include
if $EASY_DATABASE['ID'] = '': msg "cell.get_value: Ошибка. База данных не инициализирована." & exit
if $args[2] = '': msg "cell.get_value: Ошибка. Не указан идентификатор таблицы данных." & exit
if no @edb.list.is_el($EASY_DATABASE['data_tables_ids'], $args[2]): msg "cell.get_value: Ошибка. Таблица данных `<<$args[2]>>` не существует." & exit
!@pp:endif
if $args[0] = '': $args[0] = $arritem('$<<$args[2]>>', 'current_row')
if $args[1] = '': $args[1] = $arritem('$<<$args[2]>>', 'current_col')
!@pp:if(edb_fuse):include
if $args[1] = '': msg 'cell.get_value: Ошибка. Не указан идентификатор колонки.' &	exit
!@pp:endif
!@ Извлечь значение из ячейки, которая не существует не получится, однако ошибку это не вызовет
if no @edb.row.is($args[0], $args[2]): exit
!@ непосредственно извлекаем значение из ячейки
$args[23] = $arritem('$<<$args[2]>>', 'column.<<$args[1]>>.type'))
if $args[23] = 'tuple':
	%result = %arritem('%<<$args[2]>>_<<$args[1]>>', $args[0])
elseif $args[23] = 'num':
	!@ извлекаем число
	result = arritem('<<$args[2]>>_<<$args[1]>>', $args[0])
else:
	$result = $arritem('$<<$args[2]>>_<<$args[1]>>', $args[0])
end
!@ делаем строку и столбец текущими
@edb.row.set_cur($args[0])
@edb.col.set_cur($args[1])
--- edb.cell.get_value ---------------------------------
