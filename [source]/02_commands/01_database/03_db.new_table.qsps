QSP-Game Создать новую таблицу данных

!@ $0 - идентификатор таблицы данных, он же является названием массива, описывающего таблицу данных
!@ $1 - название таблицы данных
!@- $2 - управляющая конструкция
# edb.new_table
!@pp:if(edb_fuse):include
if $EASY_DATABASE['ID'] = '': msg "edb.new_table: База данных не инициализирована." & exit
if $args[0] = '': msg "edb.new_table: Не указан идентификатор таблицы данных." & exit
if instr('$%', $mid($args[0], 1, 1)): msg 'edb.new_table: Недопустимо указывать префикс типа → <<$args[0]>>.' & exit
if @edb.list.is_el($EASY_DATABASE['data_tables_ids'], $args[0]): msg "edb.new_table: Таблица данных `<<$args[0]>>` уже существует." & exit
!@pp:endif
!@ $args[2] = $lcase($args[2])
!@ Вносим идентификатор таблицы данных в список:
$EASY_DATABASE['data_tables_ids'] = @edb.list.append($EASY_DATABASE['data_tables_ids'], $args[0])
!@ увеличиваем счётчик созданных баз:
EASY_DATABASE['data_tables_counter'] += 1
!@ увеличиваем число существующих баз:
EASY_DATABASE['data_tables_number'] += 1
!@ создаём записи в массиве, описывающем таблицу данных
dynamic "
!@pp: вносим идентификатор и название таблицы:
	$<<$args[0]>>['ID']='<<$args[0]>>'
	$<<$args[0]>>['NAME']='<<$args[1]>>'
!@pp: тип колонки первичных ключей указываем в отдельной ячейке
	$<<$args[0]>>['column.id.type'] = 'ids'
!@pp: добавляем идентификатор колонки в список колонок
	$<<$args[0]>>['columns'] = 'id'
!@pp: указываем колонку в качестве текущей
	$<<$args[0]>>['current_col'] = 'id'
!@pp: указываем тип первичных ключей
	$<<$args[0]>>['primary_keys_type'] = '[rstr:16]'
"
!@ делаем созданную таблицу текущей
@edb.dt.set_cur($args[0])
--- edb.new_table ---------------------------------
