QSP-Game функция удаления таблиц данных
$args[0]...$args[18] - идентификаторы таблиц данных
# edb.bd.delTables
local dt_count=arrsize('$args')	& !@ получаем число таблиц данных к удалению
local $columns_list	& !@ массив для распарсенного списка колонок
!@ перебираем циклом все таблицы данных и удаляем
loop local i=0 while i<dt_count step i+=1:
	!@ необходимо получить список колонок из таблицы данных
	@em.str.inArr($dyneval({$result=$arritem($args[0],'columns')},$args[i]),'$columns_list','|',"[notspace][trim][strip]")
	!@ когда у нас есть распарсенный список колонок, удаляем массивы с этими колонками
	loop local j,size=0,arrsize('$columns_list') while j<size step j+=1:
		killvar '<<$args[i]>>_<<$columns_list[j]>>'
		if $EASY_DATABASE['current_data_table']=$args[i] and $EASY_DATABASE['current_col']=$columns_list[j]:
			!@ снимаем активность со столбца, если это текущий столбец
			$EASY_DATABASE['current_col']=''
		end
	end
	!@ когда удалены колонки, удаляем массив таблицы
	killvar $args[i]
	!@ убираем запись от таблице из базы данных
	EASY_DATABASE['data_table.number']-=1	& !@ уменьшаем счётчик существующих таблиц
	!@ удаляяем запись о таблице из списка идентификаторов таблиц
	$EASY_DATABASE['data_table_ids']=@edb.list.remove($EASY_DATABASE['data_table_ids'],$args[i])
	!@ снимаем активность с базы данных, если это текущая база данных
	if $EASY_DATABASE['current_data_table']=$args[i]: $EASY_DATABASE['current_data_table']=''
end
--- edb.bd.delTables ---------------------------------

Предполагается, что эта функция будет использоваться довольно редко
