QSP-Game Распечатка колонки

!@ $0 - идентификатор колонки
!@ $1 - идентификатор таблицы данных
!@ $2 - управление
# edb.col.print
if $args[1] = '': $args[1] = $EASY_DATABASE['current_data_table']
!@pp:if(edb_fuse):include
if $EASY_DATABASE['ID'] = '':	msg "col.print: Ошибка. База данных не инициализирована." & exit
if $args[1] = '':	msg 'col.print: Ошибка. Не указан идентификатор таблицы данных.' & exit
if no @edb.list.is_el($EASY_DATABASE['data_tables_ids'], $args[1]):	msg 'col.print: Ошибка. Таблица данных `<<$args[1]>>` не существует.' & exit
!@pp:endif
if $args[0] = '': $args[0] = $arritem('$<<$args[1]>>', 'current_col')
!@pp:if(edb_fuse):include
if $args[0] = '': msg 'col.print: Ошибка. Не указан идентификатор колонки.' &	exit
!@pp:endif
!@ значения по умолчанию:
$args[2] = $lcase($args[2])
!@ определяем локальные переменные:
$args[20] = ''
$args[22] = $arritem('$<<$args[1]>>', 'column.<<$args[0]>>.type')
$args[23] = @edb.pref_by_type($args[22])
$args[24] = @em.tag.get_num($args[2], 'range')
args[25] = val($strfind($args[24], '(\d+)\-(\d+)', 1))
args[26] = val($strfind($args[24], '(\d+)\-(\d+)', 2))
if args[25] < 1: args[25] = 1
if args[26] > arrsize('<<$args[1]>>_<<$args[0]>>'): args[26] = arrsize('<<$args[1]>>_<<$args[0]>>')-1
args[27] = iif(args[26], args[26], arrsize('<<$args[1]>>_<<$args[0]>>')-1)
!@ получаем содержимое колонки с помощью em.arr.print
$args[20] += 'Колонка: `<<$args[0]>>`, тип: `<<$args[22]>>`, строки с <<args[25]>> по <<args[27]>>'+@em.RN
$args[20] += @em.arr.print('<<$args[23]>><<$args[1]>>_<<$args[0]>>', '[only result]', [args[25], args[26]], '|')
!@ делаем распечатанную колонку текущей
@edb.col.set_cur($args[0])
!@ выводим в результат
$result = $args[20]
--- edb.col.print ---------------------------------

Поскольку функция работает с именами массивов, здесь не используются локальные переменные,
чтобы избежать пересечения с именами массивов.

!@ $20 - содержит текст, возвращаемый функцией
!@ $22 - тип колонки
!@ $23 - символ $, если колонка не числовая
!@ $24 - пределы из тега range в управлении.
!@ #25 - начальная позиция к выводу
!@ #26 - конечная позиция к выводу, так же #27 для форматирования строки к выводу.

Управление:
	[range:N-M] - вывод значений от элемента N до M в колонке

!@pp:if(build_easy_math!=Off):nopp
!@pp:var(em_arr_print)
!@pp:var(em_tag_getnum)
!@pp:endif