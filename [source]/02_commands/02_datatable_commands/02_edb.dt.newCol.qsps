QSP-Game Таблица Данных. Добавление Колонки
$args[0]	—	идентификатор таблицы данных
$args[1]	—	идентификатор колонки
$args[2]	—	тип колонки
# edb.dt.newCol
$args[19]=$args[19]	& !@ инициализация аргументов
if $args[2]='': $args[2]='str' else $args[2]=$lcase($args[2])
!@ дальше вычисляем индекс последней строки из размера любого массива-колонки
$args['first_col']=@em.str.getWord($arritem('$'+$args[0],'columns'),1)
!@ pl "Таблица Данных. Новая колонка. Первая колонка: "+$args[0]+"_"+$args['first_col'] & wait 0 & !@< стереть
args['last_string_number']=arrsize($args[0]+"_"+$args['first_col'])-1
!@ в независимости от того, существуют ли другие колонки, нулевая ячейка текущей колонки должна содержать её тип
dynamic "$<<$args[0]>>_<<$args[1]>>[0]='<<$args[2]>>'"
!@ если номер последней строки в любой колонке больше нуля: См. Примечание 1
if args['last_string_number']>0:
	$args['id_column']=$arritem('$'+$args[0],'id_column')
	if $args['id_column']='':
		!@ если колнка идентификаторов не существует, просто создаём последнюю ячейку в колонке
		if $args[2]='num':
			dynamic "<<$args[0]>>_<<$args[1]>>[<<args['last_string_number']>>]=0"
		else
			dynamic "$<<$args[0]>>_<<$args[1]>>[<<args['last_string_number']>>]=''"
		end
	else
		!@ если колонка идентификаторов существует, циклом генерируем ячейки для всей текущей колонки
		loop args['i']=1 while args['i']<=args['last_string_number'] step args['i']+=1:
			if $args[2]='num':
				dynamic "<<$args[0]>>_<<$args[1]>>[<<$args['id_column']>>[<<args['i']>>]]=0"
			else
				dynamic "$<<$args[0]>>_<<$args[1]>>[<<$args['id_column']>>[<<args['i']>>]]=''"
			end
		end
	end
end
!@ делаем колонку текущей
$EASY_DATABASE['current_col']=$args[1]
!@ прописываем колонку в списке колонок в таблице данных, а так же тип колонки
dynamic "
	$<<$args[0]>>['columns']=@edb.list.append($<<$args[0]>>['columns'],'<<$args[1]>>')
	$<<$args[0]>>['<<$args[1]>>','col_type']='<<$args[2]>>'
"
--- edb.dt.newCol ---

Данная локация работает с массивами, поэтому сокращаем до мимнимума число локальных переменных.

--Возможные типы колонок--

str - это колонка, которая может содержать только простую строку
dict - это колонка, содержащая словарь значений. Словарь составляется из тегов типа "[tag:строка текста:tag]".
list - это строка, содержащая перечисленные через | значения
tuple - колонка со значениями кортежами
num - колонка с числовыми значениями

Примечание 1:
	Обратите внимание. Если в указанной Таблице Данных существует колонка идентификаторов, то циклом создаются все ячейки в этой колонке. Если Таблица данных уже разрослась, это может быть очень долгий процесс.
	Создавать колонки рекомендуется на начальных этапах игры!!!!

