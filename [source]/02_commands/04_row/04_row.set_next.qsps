QSP-Game Присваивает значение ячейке текущей колонки, затем
перемещает указатель на следующую колонку

$args[0] - значение
$args[1] - идентификатор строки
$args[2] - идентификатор таблицы данных

# edb.row.set_next
if $args[2] = '': $args[2] = $EASY_DATABASE['current_data_table']
!@pp:if(edb_fuse):include
if $EASY_DATABASE['ID'] = '': msg "row.set_next: Ошибка. База данных не инициализирована." & exit
if no @edb.list.is_el($EASY_DATABASE['data_tables_ids'], $args[2]): msg "row.print: Ошибка. Таблица данных `<<$args[2]>>` не существует." & exit
if $args[2] = '': msg "row.set_nex: Ошибка. Не указан идентификатор таблицы данных." & exit
!@pp:endif
if $args[1] = '': $args[1] = $arritem('$<<$args[2]>>', 'current_row')
!@ Изменять строку, которой нет в таблице данных, нельзя, однако ошибку в игре это не вызовет.
if $arritem('$<<$args[2]>>_id', $args[1]) <> $args[1]: exit
!@ получаем идентификатор текущей колонки
$args[20] = $arritem('$<<$args[2]>>', 'current_col')
!@ получаем тип текущей колонки
$args[25] = $arritem('$<<$args[2]>>_<<$args[20]>>', 0)
!@ получаем список колонок
$args[21] = $arritem('$<<$args[2]>>', 'columns')
!@ получаем вхождение текущей колонки в список
args[22] = instr($args[21], '<<$args[20]>>|')

!@ присваиваем значение ячейке
if $args[25] = 'num':
	@edb.cell.set_value(args[0], $args[1], $args[20], $args[2])
else:
	@edb.cell.set_value($args[0], $args[1], $args[20], $args[2])
end
if args[22] = 0:
	!@ если за текущей больше нет колонок, ничего не делаем
	exit
else:
	!@ вырезаем остаток списка:
	$args[23] = $mid($args[21], args[22] + len('<<$args[20]>>|'))
	!@ получаем первый элемент списка
	$args[24] = @edb.list.first($args[23])
	!@ перемещаем указатель на следующую колонку
	@edb.col.set_cur($args[24])
end
--- edb.row.set_next ---------------------------------

$args[20] - идентификатор текущей колонки
$args[21] - список колонок в таблице данных
args[22] - вхождение подстроки с текущей колонкой в список
$args[23] - список за текущей колонкой
$args[24] - идентификатор следующей колонки
$args[25] - тип текущей колонки
