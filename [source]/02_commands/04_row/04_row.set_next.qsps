QSP-Game Присваивает значение ячейке текущей колонки, затем перемещает указатель на следующую колонку

!@ $0 - значение (кортеж, число, строка)
!@ $1 - идентификатор строки
!@ $2 - идентификатор таблицы данных
# edb.row.set_next
if $args[2] = '': $args[2] = $EASY_DATABASE['current_data_table']
!@pp:if(edb_fuse):include
if $EASY_DATABASE['ID'] = '': msg "row.set_next: Ошибка. База данных не инициализирована." & exit
if $args[2] = '': msg "row.set_nex: Ошибка. Не указан идентификатор таблицы данных." & exit
if no @edb.list.is_el($EASY_DATABASE['data_tables_ids'], $args[2]): msg "row.print: Ошибка. Таблица данных `<<$args[2]>>` не существует." & exit
!@pp:endif
!@ получаем идентификатор текущей колонки
$args[20] = $arritem('$<<$args[2]>>', 'current_col')
!@pp:if(edb_fuse):include
if $args[20] = '': msg 'col.set_next: Ошибка. Указатель текущей колонки не выставлен.' &	exit
!@pp:endif
if $args[1] = '': $args[1] = $arritem('$<<$args[2]>>', 'current_row')
!@ Изменять строку, которой нет в таблице данных, нельзя, однако ошибку в игре это не вызовет.
if no @edb.row.is($args[1], $args[2]): exit

!@ получаем список колонок
$args[21] = $arritem('$<<$args[2]>>', 'columns')

!@ получаем тип текущей колонки
$args[25] = $arritem('$<<$args[2]>>', 'column.<<$args[20]>>.type')

!@ присваиваем значение ячейке
if $args[25] = 'tuple':
	@edb.cell.set_value(%args[0], $args[1], $args[20], $args[2])
elseif $args[25] = 'num':
	@edb.cell.set_value(args[0], $args[1], $args[20], $args[2])
else:
	@edb.cell.set_value($args[0], $args[1], $args[20], $args[2])
end

!@ получаем вхождение текущей колонки в список
args[22] = instr($args[21], '<<$args[20]>>|')
if args[22] = 0:
	!@ если за текущей больше нет колонок, ничего не делаем
	exit
else:
	!@ вырезаем остаток списка:
	$args[23] = $mid($args[21], args[22] + len('<<$args[20]>>|'))
	!@ получаем первый элемент списка
	$args[24] = @edb.list.first($args[23])
	!@ перемещаем указатель на следующую колонку
	@edb.col.set_cur($args[24])
end
--- edb.row.set_next ---------------------------------

!@ $20 - идентификатор текущей колонки
!@ $21 - список колонок в таблице данных
!@ #22 - вхождение подстроки с текущей колонкой в список
!@ $23 - список за текущей колонкой
!@ $24 - идентификатор следующей колонки
!@ $25 - тип текущей колонки
