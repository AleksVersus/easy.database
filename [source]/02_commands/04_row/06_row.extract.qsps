QSP-Game Функция извлекает содержимое строки в массив

Функция извлекает из строки содержимое, помещая значения в указанный массив,
при этом индексами элементов массива выступают названия колонок.

Концепция функции взята из библиотечки Олегуса

$args[0] - название массива
$args[1] - идентификатор строки
$args[2] - идентификатор таблицы данных

# edb.row.extract
if $args[0] = '': exit & !@ нельзя извлечь данные, если массив не указан
if instr($args[0], '$') = 1: $args[0] = $mid($args[0], 2) & !@ название массива не должно содержать символ доллара
if $args[2] = '': $args[2] = $EASY_DATABASE['current_data_table']
!@pp:if(edb_fuse):include
!@ Предохранители:
if $EASY_DATABASE['ID'] = '': msg "row.extract: Ошибка. База данных не инициализирована." & exit
if $args[2] = '': msg "row.extract: Ошибка. Не указан идентификатор таблицы данных." & exit
if no @edb.list.is_el($EASY_DATABASE['data_tables_ids'], $args[2]): msg "row.extract: Ошибка. Таблица данных «<<$args[2]>>» не существует." & exit
!@pp:endif
if $args[1] = '': $args[1] = $arritem('$<<$args[2]>>', 'current_row')
!@ Извлекать данные из строки, которой нет в таблице данных, нельзя, однако ошибку в игре это не вызовет.
if $arritem('$<<$args[2]>>_id', $args[1]) <> $args[1]: exit
!@ получаем список колонок
$args[20] = $arritem('$<<$args[2]>>', 'columns')
!@ используем функцию типа колбэк для применения к каждому элементу списка
$args[21] = {
	!@ $args[0] - название колонки
	!@ $args[3] - таблица данных
	!@ $args[4] - название массива к заполнению
	!@ $args[5] - идентификатор строки
	if $arritem('$<<$args[3]>>_<<$args[0]>>', 0) = 'num':
		dynamic "<<$args[4]>>['<<$args[0]>>'] = <<$args[3]>>_<<$args[0]>>['<<$args[5]>>']"
	else:
		dynamic "$<<$args[4]>>['<<$args[0]>>'] = $<<$args[3]>>_<<$args[0]>>['<<$args[5]>>']"
	end
}
@edb.list.for_each($args[20], $args[21], '|', $args[2], $args[0], $args[1])
!@ делаем строку текущей
@edb.row.set_cur($args[1])
- edb.row.extract -

args:
	20 - список колонок
	21 - функция типа колбэк

