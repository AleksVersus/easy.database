QSP-Game Клонирование строки

!@ $0 - идентификатор строки
!@ $1 - идентификатор таблицы данных
!@ $2 - требуемый идентификатор строки. ВНИМАНИЕ!!! При использовании этой опции не учитывается дублирование первичных ключей. Используйте только когда уверены, что первичные ключи не будут дублированы.
!@ $r - идентификатор новой строки
# edb.row.clone
if $args[1] = '': $args[1] = $EASY_DATABASE['current_data_table']
!@pp:if(edb_fuse):include
if $EASY_DATABASE['ID'] = '': msg "row.clone: Ошибка. База данных не инициализирована." & exit
if no @edb.list.is_el($EASY_DATABASE['data_tables_ids'], $args[1]): msg "row.clone: Ошибка. Таблица данных `<<$args[1]>>` не существует." & exit
if $args[1] = '': msg "row.clone: Ошибка. Не указан идентификатор таблицы данных." & exit
!@pp:endif
if $args[0] = '': $args[0] = $arritem('$<<$args[1]>>', 'current_row')
if no @edb.row.is($args[0], $args[1]): exit
!@ создаём новую строку
$args[20] = @edb.dt.new_row($args[1], $args[2])
!@ --------------------- заполняем строку данными из исходной строки ---------------------
!@ список колонок
$args[21] = $arritem('$<<$args[1]>>', 'columns')
!@ колбэк
$args[22] = {
	if $args[0] = 'id': exit
	$args[23] = @edb.pref_by_type($arritem('$<<$args[3]>>', 'column.<<$args[0]>>.type'))
	dynamic "<<$args[23]>><<$args[3]>>_<<$args[0]>>['<<$args[5]>>'] = <<$args[23]>><<$args[3]>>_<<$args[0]>>['<<$args[4]>>']"
}
!@ обходим колонки
@edb.list.for_each($args[21], $args[22], $args[1], $args[0], $args[20])
!@ --------------------- заполняем строку данными из исходной строки ---------------------
!@ текущей делаем новую строку
@edb.row.set_cur($args[20])
--- edb.row.clone ---------------------------------

args:
	20 - идентификатор новой строки
	21 - список колонок
	22 - функция-колбэк, вызываемая для каждой колонки

Клонирует строку в таблице данных. Т.е. создаёт в текущей таблице данных точную копию строки, но с новым первичным ключом.

{:callback

!@ $0 - идентификатор колонки
!@ $3 - идентификатор таблицы данных
!@ $4 - идентификатор исходной строки
!@ $5 - идентификатор новой строки

}
