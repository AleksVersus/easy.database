QSP-Game Функция вводит в строку содержимое из указанного массива

!@ $0 - название массива
!@ $1 - идентификатор строки
!@ $2 - идентификатор таблицы данных
# edb.row.inject
if $args[0] = '': exit & !@ нельзя добавить данные, если массив не указан
if $args[2] = '': $args[2] = $EASY_DATABASE['current_data_table']
!@pp:if(edb_fuse):include
if $EASY_DATABASE['ID'] = '': msg "row.inject: Ошибка. База данных не инициализирована." & exit
if no @edb.list.is_el($EASY_DATABASE['data_tables_ids'], $args[2]): msg "row.inject: Ошибка. Таблица данных `<<$args[2]>>` не существует." & exit
if $args[2] = '': msg "row.inject: Ошибка. Не указан идентификатор таблицы данных." & exit
if @edb.type_prefix.is($args[0]): msg "edb.row.inject: Ошибка. Недопустимо указывать префикс типа → <<$args[0]>>." & exit
!@pp:endif
if $args[1] = '': $args[1] = $arritem('$<<$args[2]>>', 'current_row')
!@ невозможно вписать содержимое в несуществующую строку, однако ошибки это не вызовет
if no @edb.row.is($args[1], $args[2]): exit
!@ получаем список колонок
$args[20] = $arritem('$<<$args[2]>>', 'columns')
!@ используем функцию типа колбэк для применения к каждому элементу списка
$args[21] = {
	if $args[0] = 'id': exit
	$args[22] = @edb.pref_by_type(@edb.col.type($args[0], $args[3]))
	dynamic "<<$args[22]>><<$args[3]>>_<<$args[0]>>['<<$args[5]>>'] = <<$args[22]>><<$args[4]>>['<<$args[0]>>']"
}
@edb.list.for_each($args[20], $args[21], $args[2], $args[0], $args[1])
!@ делаем строку текущей
@edb.row.set_cur($args[1])
--- edb.row.inject ---------------------------------

Функция вводит в указанную строку данные из указанного массива.
При этом предполагается, что данные лежат в ячейках, индексы которых
соответствуют названиям колонок таблицы данных.

args:
!@ $20 - список колонок
!@ $21 - функция типа колбэк

{
	!@  $0 - название колонки
	!@  $3 - таблица данных
	!@  $4 - название массива к заполнению
	!@  $5 - идентификатор строки
}
