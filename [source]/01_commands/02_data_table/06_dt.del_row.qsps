QSP-Game Удаляет строку из таблицы данных по её идентификатору

$args[0] - идентификатор строки (primary key)
$args[1] - идентификатор таблицы данных

# edb.dt.del_row
if $args[1] = '': $args[1] = $EASY_DATABASE['current_data_table']
!@pp:if(edb_fuse):include
if $EASY_DATABASE['ID'] = '': msg "dt.del_row: Ошибка. База данных не инициализирована." & exit
if $args[1] = '': msg "dt.del_row: Ошибка. Не указан идентификатор таблицы данных." & exit
if no @edb.list.is_el($EASY_DATABASE['data_tables.ids'], $args[1]): msg "dt.del_row: Ошибка. Таблица данных «<<$args[1]>>» не существует." & exit
!@pp:endif
!@ Удалить строку, которой нет в таблице данных, нельзя, однако ошибку в игре это не вызовет.
if $args[0] = '' or $arritem('$<<$args[1]>>_id', $args[0]) <> $args[0]: exit
!@ вносим освободившийся ключ в список, если тип ключей порядковый:
$args[20] = $arritem('$<<$args[1]>>', 'primary_keys_type')
if instr($args[20], '[hex:') <> 0 or instr($args[20], '[num:') <> 0:
	dynamic "$<<$args[1]>>['free_keys'] = @edb.list.append($<<$args[1]>>['free_keys'], '<<$args[0]>>')"
end
!@ Удаление строки производится путём удаления ячеек из каждого массива-столбца:
$args[21] = $arritem('$<<$args[1]>>', 'columns')
$args[22] = { killvar '$<<$args[1]>>_<<$args[0]>>', $args[2] }
$args['buffer'] = @edb.list.for_each($args[21], $args[22], '|', $args[1], $args[0])
!@ если строка была текущей, снимаем указатель
if $arritem('$<<$args[1]>>', 'current_row') = $args[0]:
	dynamic "$<<$args[1]>>['current_row'] = ''"
end
!@ делаем таблицу данных текущей
@edb.dt.set_cur($args[1])
--- edb.dt.del_row ---------------------------------

$args[20] - тип первичного ключа
$args[21] - список колонок
$args[22] - функция типа "колбэк" для обработки элементов списка
