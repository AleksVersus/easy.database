QSP-Game Вывод информации о таблице данных

$args[0] - идентификатор таблицы данных
$args[1] - управление

# edb.dt.print
if $args[0] = '': $args[0] = $EASY_DATABASE['current_data_table']
!@pp:if(edb_fuse):include
if $EASY_DATABASE['ID'] = '': msg "edb.dt.print: Ошибка. База данных не инициализирована." & exit
if $args[0] = '': msg "dt.print: Ошибка. Не указан идентификатор таблицы данных." & exit
if $arritem('$<<$args[0]>>', 'ID') <> $args[0]: msg "edb.dt.print: Ошибка. Таблица данных «<<$args[0]>>» не создана." & exit
!@pp:endif
$args[1] = $lcase($args[1])
$args[20], $args[21] = '', '
'
$args[22] = $arritem('$<<$args[0]>>', 'primary_keys_type')
if instr($args[1], '[oi]') <> 0: $args[1] = $replace($args[1], '[oi]', '[only info]')
if instr($args[1], '[ot]') <> 0: $args[1] = $replace($args[1], '[ot]', '[only table]')
!@ ------------------ непосредственно информация о таблице данных --------------------
if instr($args[1], '[only info]') <> 0 or instr($args[1], '[only table]') = 0:
	$args[20] += "Идентификатор таблицы данных: '" + $arritem('$<<$args[0]>>', 'ID') + "'<<$args[21]>>"
	$args[20] += "Название таблицы данных: '" + $arritem('$<<$args[0]>>', 'NAME') + "'<<$args[21]>>"
	$args[20] += "Список колонок: '" + $arritem('$<<$args[0]>>', 'columns') + "'<<$args[21]>>"
	$args[20] += "Текущая колонка: '" + $arritem('$<<$args[0]>>', 'current_col') + "'<<$args[21]>>"
	$args[20] += "Высота: " + $str(arrsize('$<<$args[0]>>_id'))+$args[21]
	$args[20] += "Вид первичных ключей (ID строк): '" + $args[22] + "'<<$args[21]>>"
	if instr($args[22], '[hex:') <> 0 or instr($args[22], '[num:') <> 0:
		$args[20] += "Список освободившихся ключей: '" + $arritem('$<<$args[0]>>', 'free_keys') + "'<<$args[21]>>"
	end
	$args[20] += "Текущая строка: '" + $arritem('$<<$args[0]>>', 'current_row') + "'<<$args[21]>>"
	$args[23] = $dyneval('$result = $<<$args[0]>>_id[]')
	if $args[23] <> 'ids' and $args[23] <> '':
		$args[20] += "Последний первичный ключ: '" + $args[23] + "'<<$args[21]>>"
	end
end
if instr($args[1], '[only table]') <> 0 or instr($args[1], '[only info]') = 0:
	$args[24] = $arritem('$<<$args[0]>>', 'columns')
	$args[25] = {
		$result = $args[2]
		if $arritem('$<<$args[3]>>_<<$args[0]>>', 0) <> 'num':
			$result += "'$<<$args[3]>>_<<$args[0]>>',"
		else:
			$result += "'<<$args[3]>>_<<$args[0]>>',"
		end
	}
	$args[26] = @edb.list.for_each($args[24], $args[25], '|', $args[0])
	if instr($args[1], '[range:') <> 0:
		$args[27] = $strfind($args[1], '\[range:(\d+\-\d+)\]', 1)
		$args[26] += "'[range onscreen:<<$args[27]>>]'"
	else:
		$args[26] = $mid($args[26], 1, len($args[26]) - 1)
	end
	$args[20] += $dyneval("$result = @em.fewArrs.print(<<$args[26]>>)")
end
!@ ------------------ непосредственно информация о таблице данных --------------------
!@ делаем созданную таблицу текущей
@edb.dt.set_cur($args[0])
if instr($args[1], '[inmain]') <> 0:
	*pl $args[20]
elseif instr($args[1], '[instat]') <> 0:
	pl $args[20]
else:
	$result = $args[20]
end
--- edb.dt.print ---------------------------------

$args[1] - управление:
	Настройки вывода:
	[oi] или [only info] — выводится информация о таблице данных
	[ot] или [only table] — выводится только содержимое таблицы данных в виде таблицы
		[range:N-M] - выведутся только строки с N по M включительно.
	Назначение вывода:
	[inmain] - вывод в окно основного описания
	[instat] - вывод в окно дополнительного описания
	пустая строка или любое иное значение - возвращает результат

$args[20] - переменная для записывания текста возвращаемого к выводу
$args[21] - символ переноса строки
$args[22] - тип первичного ключа
$args[23] - последний первичный ключ
$args[24] - список колонок
$args[25] - функция типа колбэк для обработки элементов списка колонок
$args[26] - строка аргументов

!@pp:if(build_easy_math!=Off):nopp
!@pp:var(em_fewarrs_print)
!@pp:endif

{:for_each
	0 - list of columns
	1 - callback
	2 - |
	3 - table
	{:callback
		0 - item
		1 - tail
		2 - result
		3 - table
	}
}