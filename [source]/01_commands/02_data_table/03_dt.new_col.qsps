QSP-Game Создаём колонку в таблице данных

$args[0] - идентификатор колонки
$args[1] - тип колонки
$args[2] - идентификатор таблицы данных

# edb.dt.new_col
!@ значения по умолчанию:
if $args[1] = '': $args[1] = 'str'
if $args[2] = '': $args[2] = $EASY_DATABASE['current_data_table']
!@ предохранители:
if $EASY_DATABASE['ID'] = '':	msg "dt.new_col: Ошибка. База данных не инициализирована." & exit
if no @edb.list.is_el($EASY_DATABASE['data_tables.ids'], $args[2]):	msg 'dt.new_col: Ошибка. Таблица данных «<<$args[2]>>» не существует.' & exit
if $args[2] = '':	msg 'dt.new_col: Ошибка. Не указан идентификатор таблицы данных.' & exit
if $args[0] = '': msg 'dt.new_col: Ошибка. Не указан идентификатор колонки.' &	exit
if $args[0] = 'id' or $args[1] = 'ids':	msg 'dt.new_col: Колонка первичных ключей уже создана.' & exit
!@ создаём массив, который является колонкой, и задаём тип данных для колонки.
dynamic "$<<$args[2]>>_<<$args[0]>>[0] = '<<$args[1]>>'"
!@ если размер колонки первичных ключей больше единицы, циклом заполняем указанную колонку пустыми значениями.
!@ Внимание! Лучше заранее предусмеотреть необходимое количество колонок и создавать их все на старте игры,
!@ в противном случае, при создании колонок во время игры, данный алгоритм будет подвешивать плеер при больших объёмах данных.
local size_ = arrsize('$<<$args[2]>>_id')
local $id_
if size_ > 1:
	loop local i = 1 while i < size_ step i += 1:
		$id_ = $arritem('$<<$args[2]>>_id', i)
		if $args[1] = 'num':
			dynamic "<<$args[2]>>_<<$args[0]>>['<<$id_>>'] = 0"
		else
			dynamic "$<<$args[2]>>_<<$args[0]>>['<<$id_>>'] = ''"
		end
	end
end
!@ добавляем колонку в список колонок
dynamic "$<<$args[2]>>['columns'] = @edb.list.rappend($<<$args[2]>>['columns'], '<<$args[0]>>')"
!@ делаем колонку текущей
@edb.col.set_cur($args[0], $args[2])
--- edb.dt.new_col ---------------------------------

--Возможные типы колонок--

str - это колонка, которая может содержать только простую строку
dict - это колонка, содержащая словарь значений. Словарь составляется из тегов типа "[tag:строка текста:tag]".
list - это строка, содержащая перечисленные через | значения
tuple - колонка со значениями кортежами
num - колонка с числовыми значениями
ids - колонка первичных ключей 
