QSP-Game Удалить таблицу данных

$args[0] - идентификатор таблицы данных

# edb.del_table

if $args[0] = '': $args[0] = $EASY_DATABASE['current_data_table']
!@pp:if(edb_fuse):include
if $EASY_DATABASE['ID'] = '': msg "edb.del_table: Ошибка. База данных не инициализирована." & exit
if $args[0] = '': msg "edb.del_table: Ошибка. Не указан идентификатор таблицы данных." & exit
if no @edb.list.is_el($EASY_DATABASE['data_tables_ids'], $args[0]): msg "edb.del_table: Ошибка. Таблица данных «<<$args[0]>>» не существует." & exit
!@pp:endif

!@ проходим циклом по всем колонкам и уничтожаем массивы
$args[20] = {
	if $args[0] <> 'id' and $args[0] <> '':
		@edb.dt.del_col($args[0], $args[3])
	end
}
@edb.list.for_each($arritem('$<<$args[0]>>', 'columns'), $args[20], '|', $args[0])
!@ удаляем колонку первичных ключей
killvar '$<<$args[0]>>_id'
!@  -------------------- из базы данных удаляем записи --------------------
!@ уменьшаем счётчик существующих таблиц
EASY_DATABASE['data_tables_number'] -= 1
!@ удаляяем запись о таблице из списка идентификаторов таблиц
$EASY_DATABASE['data_tables_ids'] = @edb.list.remove($EASY_DATABASE['data_tables_ids'], $args[0])
!@ снимаем указатель с базы данных, если это текущая база данных
if $EASY_DATABASE['current_data_table'] = $args[0]: $EASY_DATABASE['current_data_table'] = ''
!@ уничтожаем массив таблицы данных
killvar $args[0]
--- edb.del_table ---------------------------------

Функция использует локальный массив $edb_bd_deltable_columns_list_. Избегайте использования данного массива в своей игре.

!@pp:if(build_easy_math!=Off):nopp
!@pp:var(em_str_inarr)
!@pp:endif
