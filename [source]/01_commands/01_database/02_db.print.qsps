QSP-Game Просмотр информации о базе данных

# edb.print
$args[0] = $lcase($args[0])
$args[20], $args[21] = '', '
'

!@ ------------------ непосредственно информация о базе данных --------------------
if $EASY_DATABASE['ID'] <> '':
	$args[20] += "Идентификатор базы данных: " + $EASY_DATABASE['ID'] + $args[21]
	$args[20] += "Идентификаторы Таблиц Данных: " + $EASY_DATABASE['data_tables_ids'] + $args[21]
	$args[20] += "Число созданных Таблиц Данных: " + EASY_DATABASE['data_tables.counter'] + $args[21]
	$args[20] += "Число существующих Таблиц Данных: " + EASY_DATABASE['data_tables.number'] + $args[21]
	$args[20] += "Текущая Таблица Данных: " + $EASY_DATABASE['current_data_table'] + $args[21]
	if instr($args[0], '[all arrays]') <> 0:
		$args[20] += "Все занятые массивы: " + $args[21]
		$args[20] += "$EASY_DATABASE" + $args[21]
		$args[22] = {
			$result = '$<<$args[0]>>' + $args[1]
			$result += @edb.list.for_each($arritem('$<<$args[0]>>', 'columns'), $args[2], '|', $args[0], $args[1])
		}
		$args[23] = {
			$result = '$<<$args[1]>>_<<$args[0]>>' + $args[2]
		}
 		$args[20] += @edb.list.for_each($EASY_DATABASE['data_tables_ids'], $args[22], '|', $args[21], $args[23])
	end
else:
	$args[20] += 'База данных не инициализирована'
end
!@ ------------------ непосредственно информация о базе данных --------------------

if instr($args[0], '[inmain]') <> 0:
	*pl $args[20]
elseif instr($args[0], '[instat]') <> 0:
	pl $args[20]
else
	$result = $args[20]
end
--- edb.print ---------------------------------

$args[0] - управление:
	[inmain] - вывод в окно основного описания
	[instat] - вывод в окно дополнительного описания
	пустая строка или любое иное значение - возвращает результат

	[all arrays] - увидим на экране названия всех массивов

$args[20] - выходной текст, возращаемый функцией к выводу
$args[21] - символ переноса строки
$args[22] - колбэк
