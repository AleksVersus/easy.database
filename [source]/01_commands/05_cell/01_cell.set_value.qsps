QSP-Game Устанавливает значение ячейки

$args[0] или args[0] - значение, присваиваемое ячейке
$args[1] - идентификатор строки
$args[2] - идентификатор колонки
$args[3] - идентификатор таблицы данных

# edb.cell.set_value
!@pp:if(fuse):include
!@ Предохранители:
if $EASY_DATABASE['ID'] = '': msg "cell.set_value: Ошибка. База данных не инициализирована." & exit
if $args[3] = '': $args[3] = $EASY_DATABASE['current_data_table']
if no @edb.list.is_el($EASY_DATABASE['data_tables.ids'], $args[3]): msg "cell.set_value: Ошибка. Таблица данных «<<$args[2]>>» не существует." & exit
if $args[3] = '': msg "cell.set_value: Ошибка. Не указан идентификатор таблицы данных." & exit
if $args[1] = '': $args[1] = $arritem('$<<$args[3]>>', 'current_row')
if $args[2] = '': $args[2] = $arritem('$<<$args[3]>>', 'current_col')
!@ Присвоить значение ячейке, которая не существует не получится, однако ошибку это не вызовет
if $arritem('$<<$args[3]>>_id', $args[1]) <> $args[1] or arrsize('$<<$args[3]>>_<<$args[2]>>') = 0: exit
!@pp:endif
!@ непосредственно присваиваем значение ячейке
if $arritem('$<<$args[3]>>_<<$args[2]>>', 0) = 'num':
	!@ присваиваем число
	dynamic "<<$args[3]>>_<<$args[2]>>['<<$args[1]>>'] = <<args[0]>>"
else:
	dynamic "$<<$args[3]>>_<<$args[2]>>['<<$args[1]>>'] = '<<$args[0]>>'"
end
!@ делаем строку и столбец текущими
@edb.row.set_cur($args[1])
@edb.col.set_cur($args[2])
--- edb.cell.set_value ---------------------------------
