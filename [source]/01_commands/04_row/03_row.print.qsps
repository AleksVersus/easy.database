QSP-Game Вывод информации о строке на экран

$args[0] - идентификатор строки
$args[1] - идентификатор таблицы данных
$args[2] - управление

# edb.row.print
!@ основные локальные переменные
local $table_, $row_, $mode_= $args[1], $args[0], $args[2]
!@ Предохранители:
if $table_ = '': $table_ = $EASY_DATABASE['current_data_table']
if $EASY_DATABASE['ID'] = '': msg "row.print: Ошибка. База данных не инициализирована." & exit
if no @edb.list.is_el($EASY_DATABASE['data_tables.ids'], $table_): msg "row.print: Ошибка. Таблица данных «<<$table_>>» не существует." & exit
if $table_ = '': msg "row.print: Ошибка. Не указан идентификатор таблицы данных." & exit
if $row_ = '': $row_ = $arritem('$<<$table_>>', 'current_row')
!@ Распечатать строку, которой нет в таблице данных, нельзя, однако ошибку в игре это не вызовет.
if $arritem('$<<$table_>>_id', $row_) <> $row_: exit
!@ объявляем дополнительные локальные переменные:
local $export_, $nr_ = '', '
'
!@ получаем список колонок:
local $column_list_ = $arritem('$<<$table_>>', 'columns')
!@ задаём описание строки
$export_ += 'Строка: <<$row_>>' + $nr_
!@ извлекаем значения из колонок
local $callback_ = {$result = $arritem('$<<$args[2]>>_<<$row_>>', $table_) + '|'}
$export_ += @edb.list.for_each($column_list_, $callback_, '|', $row_, $table_)
!@ делаем созданную таблицу текущей
@edb.dt.set_cur($table_)
!@ делаем созданную таблицу текущей
@edb.row.set_cur($row_)
!@ выводим на экран
if instr($mode_, '[inmain]') <> 0:
	*pl $export_
elseif instr($mode_, '[instat]') <> 0:
	pl $export_
else:
	$result = $export_
end
--- edb.row.print ---------------------------------

Управление:
	[inmain] — вывод значений в окно основного описания
	[instat] — вывод значений в окно дополнительного описания
