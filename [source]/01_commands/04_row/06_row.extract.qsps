QSP-Game Функция извлекает содержимое строки в массив

Функция извлекает из строки содержимое, помещая значения в указанный массив,
при этом индексами элементов массива выступают названия колонок.

Концепция функции взята из библиотечки Олегуса

$args[0] - название массива
$args[1] - идентификатор строки
$args[2] - идентификатор таблицы данных

# edb.row.extract
if instr($args[0], '$') = 1: $args[0] = $mid($args[0], 2) & !@ название массива не должно содержать символ доллара
if $args[2] = '': $args[2] = $EASY_DATABASE['current_data_table']
!@pp:if(fuse):include
!@ Предохранители:
if $EASY_DATABASE['ID'] = '': msg "row.extract: Ошибка. База данных не инициализирована." & exit
if no @edb.list.is_el($EASY_DATABASE['data_tables.ids'], $args[2]): msg "row.extract: Ошибка. Таблица данных «<<$args[2]>>» не существует." & exit
if $args[2] = '': msg "row.extract: Ошибка. Не указан идентификатор таблицы данных." & exit
!@pp:endif
if $args[1] = '': $args[1] = $arritem('$<<$args[2]>>', 'current_row')
!@ получаем список колонок
$args[20] = $arritem('$<<$args[2]>>', 'columns')
pl $args[20]
!@ используем функцию типа колбэк для применения к каждому элементу списка
$args[21] = {
	!@ $args[0] - название колонки
	!@ $args[1] - таблица данных
	!@ $args[2] - название массива к заполнению
	!@ $args[3] - идентификатор строки
	if $arritem('$<<$args[1]>>_<<$args[0]>>', 0) = 'num':
		dynamic "<<$args[2]>>['<<$args[0]>>'] = <<$args[1]>>_<<$args[0]>>['<<$args[3]>>']"
	else:
		dynamic "$<<$args[2]>>['<<$args[0]>>'] = $<<$args[1]>>_<<$args[0]>>['<<$args[3]>>']"
	end
}
@edb.list.for_each($args[20], $args[21], '|', $args[2], $args[0], $args[1])
!@ делаем строку текущей
@edb.row.set_cur($args[1])
- edb.row.extract -

args:
	20 - список колонок
	21 - функция типа колбэк

