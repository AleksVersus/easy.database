QSP-Game Заполнение строки значениями

Последний аргумент - кортеж из идентификатора строки и идентификатора таблицы данных, либо только идентификатор строки
Остальные аргументы могут быть просто значениями, или кортежами из идентификаторов столбцов и значений.

# edb.row.fill
!@pp:if(edb_fuse):include
if $EASY_DATABASE['ID'] = '': msg "row.fill: Ошибка. База данных не инициализирована." & exit
!@pp:endif
args[20] = arrsize('$args')
$args[21], $args[22] = $args[args[20]-1]
args[23] = 2
if no @edb.list.is_el($EASY_DATABASE['data_tables_ids'], $args[22]):
	!@ если последний аргумент не содержит идентификатор таблицы данных,
	!@ значит он содержит либо только идентификатор строки, либо значение
	!@ для размещения в строке:
	$args[22] = $EASY_DATABASE['current_data_table']
	args[23] -= 1
end
!@pp:if(edb_fuse):include
if $args[22] = '': msg "row.fill: Ошибка. Не указан идентификатор таблицы данных." & exit
!@pp:endif
if $arritem('$<<$args[22]>>_id', $args[21]) <> $args[21]:
	!@ если последний аргумент не содержит идентификатор строки,
	!@ значит он содержит значение для размещения в строке
	$args[21] = $arritem('$<<$args[22]>>', 'current_row')
	args[23] -= 1
end
!@pp:if(edb_fuse):include
if $args[21] = '': msg "row.fill: Ошибка. Не указан идентификатор строки." & exit
!@pp:endif
!@ если оказалось, что последний аргумент содержит как минимум один из идентификаторов, уничтожаем его
if args[23]:	args[20] -= 1
$args[24] = @edb.list.remove($arritem('$<<$args[22]>>', 'columns'), 'id')
local $edb_row_fill_columns_
@em.str.inArr($args[24], '$edb_row_fill_columns_', '|')
loop args[29] = 0 while args[29] < arrsize('$edb_row_fill_columns_') and args[29] < args[20] step args[29] += 1:
	$args[25], $args[26] = $args[args[29]]
	if $args[25] = $args[26] or no @edb.list.is_el($args[24], $args[26]):
		$args[26] = $edb_row_fill_columns_[args[29]]
		args[28] = 1
	else
		args[28] = 0
	end
	$args[27] = $arritem("$<<$args[22]>>_<<$args[26]>>", 0)
	if $args[27] = 'num':
		if args[28]: $args[25] = $str(args[args[29]])
		dynamic "<<$args[22]>>_<<$args[26]>>['<<$args[21]>>'] = <<$args[25]>>"
	else:
		dynamic "$<<$args[22]>>_<<$args[26]>>['<<$args[21]>>'] = '<<$args[25]>>'"
	end
end
!@ Не помечаем изменяемые колонки, как текущие, поскольку может статься так,
!@ что мы не будем знать, на какой колонке закончилось заполнение.
!@ помечаем текущую строку, как текущую:
@edb.row.set_cur($args[21], $args[22])
--- edb.row.fill ---------------------------------

Внимание! Функция работает с массивами, поэтому большая часть локальных переменных заменена на значения массива args, дабы избежать пересечения имён.

Функция использует локальный массив $edb_row_fill_columns_. Избегайте использовать массивы с таким названием в своей игре.

args[20] — размер массива args на момент старта функции. То есть содержит строго число переданных аргументов.
$args[21] - первичный ключ
$args[22] - идентификатор таблицы данных
args[23] - kill_last_, переменная для отслеживания, расположены ли в последнем элементе идентификаторы.
$args[24] - $columns_list_, список колонок к заполнению
$args[25] - значение, которое хотим разместить в строке
$args[26] - название колонки, в которой хотим разместить значение
$args[27] - тип колонки, в которой хотим разместить значение
args[28] - маркер для определения одно значение в аргументе, или нет.
args[29] - счётчик в цикле

!@pp:if(build_easy_math!=Off):nopp
!@pp:var(em_str_inarr)
!@pp:endif
