QSP-Game Клонирование строки

Клонирует строку в таблице данных. Т.е. создаёт в текущей таблице данных точную копию строки,
но с новым первичным ключом.

$args[0] - идентификатор строки
$args[1] - идентификатор таблицы данных

Возвращает идентификатор новой строки

# edb.row.clone
if $args[1] = '': $args[1] = $EASY_DATABASE['current_data_table']
!@pp:if(edb_fuse):include
if $EASY_DATABASE['ID'] = '': msg "row.clone: Ошибка. База данных не инициализирована." & exit
if no @edb.list.is_el($EASY_DATABASE['data_tables_ids'], $args[1]): msg "row.clone: Ошибка. Таблица данных «<<$args[1]>>» не существует." & exit
if $args[1] = '': msg "row.clone: Ошибка. Не указан идентификатор таблицы данных." & exit
!@pp:endif
if $args[0] = '': $args[0] = $arritem('$<<$args[1]>>', 'current_row')
!@ Клонировать строку, которой нет в таблице данных, нельзя, однако ошибку в игре это не вызовет.
if $arritem('$<<$args[1]>>_id', $args[0]) <> $args[0]: exit
!@ создаём новую строку
$args[20] = @edb.dt.new_row($args[1])
!@ --------------------------- заполняем строку данными из исходной строки ---------------------------
!@ список колонок
$args[21] = $arritem('$<<$args[1]>>', 'columns')
!@ колбэк
$args[22] = {
!@pp:$args[0] - идентификатор колонки
!@pp:$args[1] - идентификатор таблицы данных
!@pp:$args[2] - идентификатор исходной строки
!@pp:$args[3] - идентификатор новой строки
if $arritem('$<<$args[1]>>_<<$args[0]>>', 0) = 'ids' or $args[0] = 'id':
	!@ pass. Ничего не делаем для столбца первичных ключей
elseif $arritem('$<<$args[1]>>_<<$args[0]>>', 0) = 'num':
	dynamic "<<$args[1]>>_<<$args[0]>>['<<$args[3]>>'] = <<$args[1]>>_<<$args[0]>>['<<$args[2]>>']"
else:
	dynamic "$<<$args[1]>>_<<$args[0]>>['<<$args[3]>>'] = $<<$args[1]>>_<<$args[0]>>['<<$args[2]>>']"
end
}
!@ обходим колонки
@edb.list.for_each($args[21], $args[22], '|', $args[1], $args[0], $args[20])
!@ --------------------------- заполняем строку данными из исходной строки ---------------------------
!@ текущей делаем новую страну
@edb.row.set_cur($args[20])
--- edb.row.clone ---------------------------------

args:
	20 - идентификатор новой строки
	21 - список колонок
	22 - функция-колбэк, вызываемая для каждой колонки
