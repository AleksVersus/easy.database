QSP-Game Функция вводит в строку содержимое из указанного массива

Функция вводит в указанную строку данные из указанного массива.
При этом предполагается, что данные лежат в ячейках, индексы которых
соответствуют названиям колонок таблицы данных.

$args[0] - название массива
$args[1] - идентификатор строки
$args[2] - идентификатор таблицы данных

# edb.row.inject
if $args[0] = '': exit & !@ нельзя добавить данные, если массив не указан
if instr($args[0], '$') = 1: $args[0] = $mid($args[0], 2) & !@ название массива не должно содержать символ доллара
if $args[2] = '': $args[2] = $EASY_DATABASE['current_data_table']
!@pp:if(edb_fuse):include
if $EASY_DATABASE['ID'] = '': msg "row.inject: Ошибка. База данных не инициализирована." & exit
if no @edb.list.is_el($EASY_DATABASE['data_tables.ids'], $args[2]): msg "row.inject: Ошибка. Таблица данных «<<$args[2]>>» не существует." & exit
if $args[2] = '': msg "row.inject: Ошибка. Не указан идентификатор таблицы данных." & exit
!@pp:endif
if $args[1] = '': $args[1] = $arritem('$<<$args[2]>>', 'current_row')
!@ невозможно вписать содержимое в несуществующую строку, однако ошибки это не вызовет
if $arritem('$<<$args[2]>>_id', $args[1]) <> $args[1]: exit
!@ получаем список колонок
$args[20] = $arritem('$<<$args[2]>>', 'columns')
!@ используем функцию типа колбэк для применения к каждому элементу списка
$args[21] = {
	!@ $args[0] - название колонки
	!@ $args[1] - таблица данных
	!@ $args[2] - название массива к заполнению
	!@ $args[3] - идентификатор строки
	if $args[0] = 'id': exit
	if $arritem('$<<$args[1]>>_<<$args[0]>>', 0) = 'num':
		dynamic "<<$args[1]>>_<<$args[0]>>['<<$args[3]>>'] = <<$args[2]>>['<<$args[0]>>']"
	else:
		dynamic "$<<$args[1]>>_<<$args[0]>>['<<$args[3]>>'] = $<<$args[2]>>['<<$args[0]>>']"
	end
}
@edb.list.for_each($args[20], $args[21], '|', $args[2], $args[0], $args[1])
!@ делаем строку текущей
@edb.row.set_cur($args[1])
- edb.row.inject -

args:
	20 - список колонок
	21 - функция типа колбэк

