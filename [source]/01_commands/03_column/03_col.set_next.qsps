QSP-Game Присваивает значение ячейке текущей строки, затем
перемещает указатель на следующую строку

ВНИМАНИЕ!!! Данная функция использует arrpos для поиска элементов. Для баз больших объёмов это может существенно подвешивать игру. Не используйте данную функцию без необходимости.

$args[0] - значение
$args[1] - идентификатор колонки
$args[2] - идентификатор таблицы данных

# edb.col.set_next
if $args[2] = '': $args[2] = $EASY_DATABASE['current_data_table']
!@pp:if(fuse):include
if $EASY_DATABASE['ID'] = '':	msg "col.set_next: Ошибка. База данных не инициализирована." & exit
if $args[2] = '':	msg 'col.set_next: Ошибка. Не указан идентификатор таблицы данных.' & exit
if no @edb.list.is_el($EASY_DATABASE['data_tables.ids'], $args[2]):	msg 'col.set_next: Ошибка. Таблица данных «<<$args[2]>>» не существует.' & exit
!@pp:endif
if $args[1] = '': $args[1] = $arritem('$<<$args[2]>>', 'current_col')
!@pp:if(fuse):include
if $args[1] = '': msg 'col.set_next: Ошибка. Не указан идентификатор колонки.' &	exit
if no @edb.list.is_el($arritem('$<<$args[2]>>', 'columns'), $args[1]): msg 'col.set_next: Ошибка. Неверно указан идентификатор колонки «<<$args[1]>>».' & exit
!@pp:endif
!@ получаем тип текущей колонки
$args[22] = $arritem("$<<$args[2]>>_<<$args[1]>>", 0)
!@ нельзя установить колонку первичных ключей текущей, однако это не приводит к ошибке
if $args[22] = 'ids': exit
$args[23] = $arritem('$<<$args[2]>>', 'columns')
!@ получаем идентификатор текущей строки
$args[20] = $arritem('$<<$args[2]>>', 'current_row')
!@ получаем позицию в таблитце текущей строки
args[21] = arrpos('$<<$args[2]>>_id', $args[20])
!@ выставляем значение
if $args[22] = 'num':
	@edb.cell.set_value(args[0], $args[20], $args[1], $args[2])
else:
	@edb.cell.set_value($args[0], $args[20], $args[1], $args[2])
end
if args[21] = arrsize('$<<$args[2]>>_id')-1:
	!@ если номер текущей ячейки соответствует последнему номеру строки, указатель не перемещаем
	exit
else:
	!@ в противном случае перемещаем указатель
	@edb.row.set_cur($arritem('$<<$args[2]>>_id', args[21]+1))
end
--- edb.col.set_next ---------------------------------

$args[]:

20 - идентификатор текущей строки
21 - позиция текущей строки в таблице данных
22 - тип колонки
23 - список колонок


