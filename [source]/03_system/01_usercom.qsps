QSP-Game Обработчик строки ввода для модуля easy.database

!@ $0 - управление
# edb.usercom
!@ включение-выключение
if $lcase($args[0]) = 'on':
	$usercom['edb.usercom'] = 'edb.usercom'
	exit
elseif $lcase($args[0]) = 'off':
	killvar '$usercom', 'edb.usercom'
	exit
end

if instr($user_text, 'edb') = 1:
	local $command_line_ = $trim($mid($user_text, 4))
	if $command_line_ = 'help':
		*pl 'Все команды для работы с модулем из строки ввода должны начинаться с ключевого слова `edb`'
		*pl 'Основные команды для работы с модулем (ключевое слово `edb` опущено):'
		*pl '* help - вывод справки по модулю `easy.database`.'
		*pl '* run - выполнить следующий после `run` текст, как строку кода QSP.'
		*pl '* version — текущая версия модуля.'
		*pl '* em-version — требуемая и текущая версии easy.math.'
		*pl '* gen id - генерирует uuid.'
		*pl '* init - инициализирует базу данных'
		*pl '* > - выводит информацию о базе данных'

	elseif $command_line_ = 'version':
		*pl @edb.version('str')

	elseif $command_line_ = 'em-version':
		@edb.em.version()
		
	elseif $command_line_ = 'gen id':
		*pl @em.gen_uuid()

	elseif $command_line_ = 'test':
		@edb.usercom.test()

	elseif $command_line_ = 'long test':
		@edb.usercom.test('waiting')

	elseif $command_line_ = 'init':
		@edb.init()
		*pl 'Инициализирована база данных. id = ' + $EASY_DATABASE['ID']
	elseif $command_line_ = '>':
		*pl @edb.print('[all arrays]')
	elseif instr($command_line_, '>') = 1:
		dynamic {
			local $commands_
			scanstr '$commands_', $mid($command_line_, 2), '[^|]+'
			if $commands_[0] = 'new_table' and $commands_[1] <> '':
				@edb.new_table($commands_[1])
				*pl "Создана таблица данных `<<$commands_[1]>>`."
			elseif $commands_[0] = 'del_table' and $commands_[1] <> '':
				@edb.del_table($commands_[1])
				*pl "Удалена таблица данных `<<$commands_[1]>>`."
			elseif @edb.list.is_el($EASY_DATABASE['data_tables_ids'], $commands_[0]):
				if $commands_[1] = 'print':
					*pl @edb.dt.print($commands_[0])
				end
			end
		}
	elseif instr($command_line_, 'run ') = 1:
		dynamic $trim($mid($command_line_, 4))
	end
end
--- edb.usercom ---------------------------------


$0 - может быть передано управление:
	- 'on' — подключает обработчик строки ввода к игре. Обработчик подключается независимо, т.е. не влияет на другие обработчики.
	- 'off' — отключает обработчик. Отключается только данный обработчик