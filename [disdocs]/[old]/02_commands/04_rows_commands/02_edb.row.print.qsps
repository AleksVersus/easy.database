QSP-Game вывод содержимого строки
$args[0]	—	идентификатор таблицы данных
$args[1]	—	идентификатор строки
$args[2]	—	 управление
# edb.row.print
$args[2]=$lcase($args[2])	& !@ переводим управление в нижний регистр
if instr($args[2],'[inres]')<>0: $args[2]=$replace($args[2],'[inres]','[inresult]')
$args['res'],$args['nr']='','
'
local $edb_row_print_column_list
$args['columns_list']=$arritem('$'+$args[0],'columns')	& !@ список колонок в таблице данных
pl $args['columns_list'] & wait 100 	& !@< выводим
@em.str.inArr($args['columns_list'],'$edb_row_print_column_list','|',"[notspace][trim][strip]")
if instr($args[2],'[idtype:')=0: args['idtype']=iif(@eedb.row.is_num($args[1]),1,2) else args['idtype']=val(@em.tag.getNum($args[2],'idtype'))
if instr($args[2],'[notable]')=0: $args['res']+='<table border=1 cellpadding=5><tramplin1></tramplin1><tramplin2></tramplin2>'
!@ ----------------- непосредственно сборка информациии в переменную ----------------
	loop args['i']=0 while args['i']<arrsize('$edb_row_print_column_list') step args['i']+=1:
		!@ если столбец заполнен значением отличным от значения по умолчанию, добавляем его в список
		$args['type']=$arritem('$'+$args[0]+'_'+$edb_row_print_column_list[args['i']],0)
		pl $args['type'] & wait 100 	& !@< выводим
		if $args['type']='num':
			!@ получаем число из колонки
			if args['idtype']=2:
				!@ по идентификатору
				dynamic "pl $str(<<$args[0]+'_'+$edb_row_print_column_list[args['i']]>>['<<$args[1]>>'])+' : <<$args[0]+'_'+$edb_row_print_column_list[args['i']]>>[<<$args[1]>>]'"
				$args['column_value']=$str(arritem($args[0]+'_'+$edb_row_print_column_list[args['i']],$args[1]))
			else
				!@ по числу
				$args['column_value']=$str(arritem($args[0]+'_'+$edb_row_print_column_list[args['i']],val($args[1])))
			end
		else
			!@ получаем строку из колонки
			if args['idtype']=2:
				!@ по идентификатору
				$args['column_value']=$arritem('$'+$args[0]+'_'+$edb_row_print_column_list[args['i']],$args[1])
			else
				!@ по числу
				$args['column_value']=$arritem('$'+$args[0]+'_'+$edb_row_print_column_list[args['i']],val($args[1]))
			end
		end
		pl $args['column_value'] & wait 100 	& !@< выводим
		if instr($args[2],'[notable]')=0:
			$args['tramplin1']=$strfind($args['res'],'<tramplin1>([\s\S]*?)</tramplin1>')
			$args['tramplin2']=$strfind($args['res'],'<tramplin2>([\s\S]*?)</tramplin2>')
			$args['newtr1']='<tramplin1>'+$strfind($args['res'],'<tramplin1>([\s\S]*?)</tramplin1>',1)+"<td><<$edb_row_print_column_list[args['i']]>></td>"+'</tramplin1>'
			$args['newtr2']='<tramplin2>'+$strfind($args['res'],'<tramplin2>([\s\S]*?)</tramplin2>',1)+"<td><<$args['column_value']>></td>"+'</tramplin2>'
			$args['res']=$replace($args['res'],$args['tramplin1'],$args['newtr1'])
			$args['res']=$replace($args['res'],$args['tramplin2'],$args['newtr2'])
		else
			$args['res']+=$edb_row_print_column_list[args['i']]+$args['nr']+$args['column_value']+$args['nr']
		end
	end
!@ ----------------- непосредственно сборка информациии в переменную ----------------
if instr($args[2],'[notable]')=0:
	$args['res']=$replace($args['res'],'<tramplin1>','<tr>')
	$args['res']=$replace($args['res'],'</tramplin1>','</tr>')
	$args['res']=$replace($args['res'],'<tramplin2>','<tr>')
	$args['res']=$replace($args['res'],'</tramplin2>','</tr>')
end
if instr($args[2],'[notable]')=0: $args['res']+='</table>'
if $args[2]='' or instr($args[2],'[inmain]')<>0:
	*pl $args['res']
elseif instr($args[2],'[instat]')<>0:
	pl $args['res']
elseif instr($args[2],'[inresult]')<>0:
	$result=$args['res']
else
	*pl $args['res']
end
--- edb.row.print ---------------------------------

$args[2]:
	пустая строка - вывод в окно основного описания
	[inmain] - вывод в окно основного описания
	[instat] - вывод в окно дополнительного описания
	[inresult] или [inres] - просто всё помещаем в $result
	[notable] — выводит значения не в виде таблицы
	[idtype:0] - число (1) или идентификатор (2)
