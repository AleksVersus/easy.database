QSP-Game Таблица Данных. Добавление Колонки
$args[0]	—	идентификатор колонки
$args[1]	—	тип колонки
$args[2]	—	идентификатор таблицы данных
# edb.dt.new_col
if $args[2] = '': $args[2] = $EASY_DATABASE['current_data_table']
if $args[2] = '': msg 'dt.newCol: Не выбрана таблица данных.' & exit
if $lcase($args[1])='ids' or $lcase($args[0])='id': msg 'dt.newCol: колонка первичных ключей уже создана.' & exit
$args[1] = $iif($args[1]='', 'str', $lcase($args[1]))
!@ поскольку колонка первичных ключей создаётся в таблице данных автоматически,
!@ по размеру этой колонки мы можем вычислить номер последней строки:
args['last_string_number'] = arrsize($args[2]+"_id")-1
!@ в независимости от того, существуют ли другие колонки, нулевая ячейка текущей колонки должна содержать её тип
dynamic "$<<$args[2]>>_<<$args[0]>>[0]='<<$args[1]>>'"
!@ если номер последней строки в любой колонке больше нуля: См. Примечание 1
if args['last_string_number']>0:
	$args['id_column'] = '$'+$args[2]+'_id'
	!@ циклом генерируем ячейки для всей текущей колонки, опираясь на колонку первичных ключей
	loop args['i']=1 while args['i']<=args['last_string_number'] step args['i']+=1:
		if $args[1]='num':
			dynamic "<<$args[2]>>_<<$args[0]>>[<<$args['id_column']>>[<<args['i']>>]]=0"
		else
			dynamic "$<<$args[2]>>_<<$args[0]>>[<<$args['id_column']>>[<<args['i']>>]]=''"
		end
	end
end
!@ делаем колонку текущей
$EASY_DATABASE['current_col'] = $args[0]
!@ прописываем колонку в списке колонок в таблице данных, а так же тип колонки
dynamic "
	$<<$args[2]>>['columns']=@edb.list.append($<<$args[2]>>['columns'],'<<$args[0]>>')
	$<<$args[2]>>['<<$args[0]>>','col_type']='<<$args[1]>>'
"
--- edb.dt.new_col ---

Данная локация работает с массивами, поэтому сокращаем до мимнимума число локальных переменных.

--Возможные типы колонок--

str - это колонка, которая может содержать только простую строку
dict - это колонка, содержащая словарь значений. Словарь составляется из тегов типа "[tag:строка текста:tag]".
list - это строка, содержащая перечисленные через | значения
tuple - колонка со значениями кортежами
num - колонка с числовыми значениями
ids - колонка первичных ключей

Примечание 1:
	Внимание!!! Поскольку для любой таблицы данных всегда генерируется колонка первичных ключей, добавление новых колонок в процессе работы базы
