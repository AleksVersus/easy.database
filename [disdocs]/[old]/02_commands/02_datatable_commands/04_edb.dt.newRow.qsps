QSP-Game процедура, добавляющая строку в таблицу
$args[0] - идентификатор таблицы данных
$args[1] - $args[18] - данные, которые необходимо добавить в таблицу
Последний аргумент может содержать управляющую конструкцию, тогда он должен оканчиваться ключом /dw
# edb.dt.newRow
local $edb_dt_newrow_column_list
args['osize']=arrsize('args')	& !@ число аргументов, переданных в функцию
if instr($args[args['osize']-1],'/dw')=len($args[args['osize']-1])-2 and args['osize']>2:
	!@ если в последнем аргументе управляющая конструкция, значит имеем дело со списком колонок к заполнению
	$args['first']=@edb.slice($mid($args[args['osize']-1],1,len($args[args['osize']-1])-3))
	$args['second']=@edb.list.diff($arritem('$'+$args[0],'columns'),$args['first'])
	$args['column_list']=$args['first']+'|'+$args['second']
	$args[args['osize']-1]=''
	args['osize']-=1	& !@ поскольку последний аргумент мы исключаем из обработки,
else
	$args['column_list']=$arritem('$'+$args[0],'columns')
end
!@ помещаем все значения списка в массив
@em.str.inArr($args['column_list'],'$edb_dt_newrow_column_list','|',"[notspace][trim][strip]")
$args['id_column_name']=$arritem('$'+$args[0],'id_column')
!@ теперь три варианта:
if $args['id_column_name']='':
	!@ без опоры на идентификатор
	:no_id_column
	loop args['i'],args['size']=0,arrsize('$edb_dt_newrow_column_list') while args['i']<args['size'] step args['i']+=1:
		$args['type']=$arritem('$'+$args[0],($edb_dt_newrow_column_list[args['i']],'col_type'))
		!@ pl "edb.dt.newRow: [<<args['i']>>] <<$args['type']>> <<$args[args['i']+1]>>" & wait 100 	& !@< выводим
		if args['i']<args['osize']:
			$args['string']=$args[args['i']+1]
			args['number']=val($args[args['i']+1])
		else
			$args['string']=''
			args['number']=0
		end
		if $args['type']<>'num':
			dynamic "
				$<<$args[0]>>_<<$edb_dt_newrow_column_list[args['i']]>>[]=$args[0]
			",$args['string']
		else
			dynamic "
				<<$args[0]>>_<<$edb_dt_newrow_column_list[args['i']]>>[]=args[0]
			",args['number']
		end
	end
	jump 'end_keep'
else
	!@ с опорой на айди
	!@ необходимо получить айди.
	if args['osize']=2:
		!@ если переданы всего два аргумента, значит передан только идентификатор таблицы данных и айди строки
		$args['id']=$args[1]
	else
		!@ если передано большее число аргументов, пытаемся выжать айди
		!@ колонка айди должна быть указана в определённом месте списка колонок
		args['id_column_pos']=arrpos('$edb_dt_newrow_column_list',$args['id_column_name'])
		if args['id_column_pos']<>-1 and args['id_column_pos']<args['osize']:
			!@ если в списке есть эта колонка (она должна там быть по-хорошему)
			$args['id']=$args[args['id_column_pos']+1]	& !@ назначаем айди из аргумента, переданного в эту колонку
		else
			$args['id']=''
		end
	end
	!@ pl "edb.dt.newRow: id_column_pos=<<args['id_column_pos']>> <<$args['id']>>" & wait 0 & !@< стереть
	if $args['id']='':
		!@ если айди не вычислился, это полная жопа. Вы где-то накосячили, и вся ваша таблица данных работает через хуй
		msg "edb.dt.newRow: Ошибка! Колонка для идентификаторов определена, но не существует! «"+$args['id_column_name']+"» Либо вы не определили идентификатор «<<$args[args['id_column_pos']+1]>>»|«<<$args[1]>>»"
		jump 'no_id_column'
	end
	loop args['i'],args['size']=0,arrsize('$edb_dt_newrow_column_list') while args['i']<args['size'] step args['i']+=1:
		$args['type']=$arritem('$'+$args[0],($edb_dt_newrow_column_list[args['i']],'col_type'))
		!@ pl "edb.dt.newRow: [<<args['i']>>] <<$args['type']>> <<$args[args['i']+1]>>" & wait 100 	& !@< выводим
		if args['i']<args['osize']:
			$args['string']=$args[args['i']+1]
			args['number']=val($args[args['i']+1])
		else
			$args['string']=''
			args['number']=0
		end
		if $args['type']<>'num':
			dynamic "
				$<<$args[0]>>_<<$edb_dt_newrow_column_list[args['i']]>>['<<$args['id']>>']=$args[0]
			",$args['string']
		else
			dynamic "
				<<$args[0]>>_<<$edb_dt_newrow_column_list[args['i']]>>['<<$args['id']>>']=args[0]
			",args['number']
		end
	end
	jump 'end_keep'
end
:end_keep
!@ устанавливам текущий айди и текущий номер строки
if $args['id_column_name']<>'':
	!@ текущий айди
	$EASY_DATABASE['current_id']=$arritem('$'+$args[0]+'_'+$args['id_column_name'],arrsize('$'+$args[0]+'_'+$args['id_column_name'])-1)
end
!@ текущий номер
$EASY_DATABASE['current_row']=$str(arrsize('$'+$args[0]+'_'+$args['id_column_name'])-1)
--- edb.dt.newRow ---------------------------------

Примечание 1.
	Данные из аргументов добавляются в колонки соответственно списку, если перечислены без управляющих конструкций

Примечание 2.
	Управляющая конструкция может содержать список идентификаторов колонок в том порядке, в каком нужно добавить приведённые данные:

		[id|body|kolvo|position]/dw

	Все прочие колонки заполняются пустыми значениями, соответствующими типу.

	Если указан только один аргумент и для таблицы определена колонка идентификаторов, считается, что это именно идентификатор.

